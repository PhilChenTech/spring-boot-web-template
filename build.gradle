
plugins {
    id 'org.springframework.boot' version '3.2.1' apply false
    id 'io.spring.dependency-management' version '1.1.2' apply false
    id 'java'
}

allprojects {
    group = 'com.nicenpc'
    version = '1.0.0'
    
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '21'
    targetCompatibility = '21'

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    dependencies {
        // Lombok 依賴 - 在所有子專案中可用
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        
        // MapStruct 依賴 - 在所有子專案中可用
        implementation 'org.mapstruct:mapstruct:1.5.5.Final'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
        
        // MapStruct 與 Lombok 整合配置
        annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
        
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    }

    test {
        useJUnitPlatform()
    }
}
